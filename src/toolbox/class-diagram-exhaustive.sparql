SELECT DISTINCT ?class ?icount ?property ?dt_or_class ?multiValued WHERE {
  { SELECT ?class (count(distinct ?t) as ?icount)
    WHERE {
      ?t a ?class .
    } GROUP BY ?class ORDER BY desc(?icount)
  }
  ?thing a ?class 
  OPTIONAL {
    { ?thing ?property ?v .
      FILTER (isLiteral(?v))
      BIND (datatype(?v) as ?dt_or_class)
    } UNION {
      ?thing ?property ?v .
      FILTER (isURI(?v) || isBlank(?v))
      OPTIONAL {
        ?v a ?dt_or_class .
      }
    }
  } OPTIONAL {
    FILTER EXISTS {
       ?t2    a ?class .
       ?t2    ?property ?v1,?v2 
       FILTER (?v1 != ?v2)
    }
    BIND (true as ?multiValued)
  }
} ORDER BY desc(?icount) ?class ?property ?dt_or_class