SELECT DISTINCT ?class ?icount ?property ?dt_or_class ?multiValued WHERE {
  { SELECT ?class (sample(?t) as ?thing) (count(distinct ?t) as ?icount)
    WHERE {
      ?t a ?class .
    } GROUP BY ?class ORDER BY desc(?icount)
  }
  OPTIONAL {
    { ?thing ?property ?v .
      FILTER (isLiteral(?v))
      BIND (datatype(?v) as ?dt_or_class)
    } UNION {
      ?thing ?property ?v .
      FILTER (isURI(?v) || isBlank(?v))
      OPTIONAL {
        ?v a ?dt_or_class .
      }
    }
  } OPTIONAL {
    FILTER EXISTS {
       ?thing ?property ?v,?v2 
       FILTER (?v != ?v2)
    }
    BIND (true as ?multiValued)
  }
} ORDER BY desc(?icount) ?class ?property ?dt_or_class 
